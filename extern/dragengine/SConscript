import os
import sys
import SCons

from SConsCommon import *
from SConsExternCommon import *

Import('globalEnv')

envLibrary = globalEnv.Clone()

dragengineSrcVersion = 'nightly'
srcdir = 'dragengine-{}-linux64'.format(dragengineSrcVersion)
srcdirDev = 'dragengine-dev-{}-linux64'.format(dragengineSrcVersion)

libDragengineVersion = 'nightly'
libLinkVersion = '9999'
libLinkNameDragengine = 'dragengine'
libLinkNameDELauncher = 'delauncher'

libFileNameDragengine = envLibrary.subst(
	'usr/lib/${{SHLIBPREFIX}}{}${{SHLIBSUFFIX}}.{}'.format(
		libLinkNameDragengine, libLinkVersion))

libFileNameDELauncher = envLibrary.subst(
	'usr/lib/${{SHLIBPREFIX}}{}${{SHLIBSUFFIX}}.{}'.format(
		libLinkNameDELauncher, libLinkVersion))

baseUrl = 'https://github.com/LordOfDragons/dragengine/releases/download'

nodeArtifacts = envLibrary.DownloadArtifactHelper(
	'{}.tar.bz2'.format(srcdir), 'nightly', 'lib_dragengine_fetch', baseUrl=baseUrl)
nodeArtifactsDev = envLibrary.DownloadArtifactHelper(
	'{}.tar.bz2'.format(srcdirDev), 'nightly', 'lib_dragengine2_fetch', baseUrl=baseUrl)

if 'libdragengine_includes' in globalEnv and globalEnv['libdragengine_includes']:
	globalEnv.Append(CPPPATH = [globalEnv['libdragengine_includes']])

if 'libdragengine_libraries' in globalEnv and globalEnv['libdragengine_libraries']:
	globalEnv.Append(LIBPATH = [globalEnv['libdragengine_libraries']])

def dragengineUnpack(target, source, env):
	untarArchive(target[0].get_dir().up().up().abspath, source[0].abspath)

targets = ['sdk/include/dummy']
with open(envLibrary.File('header_file_list/dragengine').srcnode().abspath, 'r') as f:
	targets.extend([x[:-1] for x in f.readlines()])
with open(envLibrary.File('header_file_list/delauncher').srcnode().abspath, 'r') as f:
	targets.extend([x[:-1] for x in f.readlines()])

headers = envLibrary.Command(targets, nodeArtifactsDev,
	envLibrary.Action(dragengineUnpack, 'Unpack Dragengine Headers'))

targets = [libFileNameDragengine, libFileNameDELauncher]
libraries = envLibrary.Command(targets, nodeArtifacts,
	envLibrary.Action(dragengineUnpack, 'Unpack Dragengine Libraries'))

envLibrary.Clean(libraries, ['sdk', 'usr', srcdir, srcdirDev])

globalEnv.Append(CPPPATH = [envLibrary.Dir('sdk/include')])
globalEnv.Append(LIBPATH = [envLibrary.Dir('usr/lib')])

globalEnv.Append(LIBS=libraries)
