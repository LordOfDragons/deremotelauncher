import os, sys, shlex
from ternary_variable import TernaryVariable

Import('globalEnv')

versionString = globalEnv['version']

envLibrary = globalEnv.Clone()

applyEnvVars = []
applyEnvVars.append('CFLAGS')
applyEnvVars.append('CPPFLAGS')
applyEnvVars.append('CXXFLAGS')
applyEnvVars.append('LDFLAGS')

# Haiku: The PATH found by SCons are wrong in many ways causing binaries to be not found.
#        Replace them with sane values. This is not a 'good' solution but should work.
if envLibrary['TARGET_PLATFORM'] == 'beos':
	import os
	envLibrary['ENV']['PATH'] = os.environ['PATH']

# set global construction variables
for x in applyEnvVars:
	if x in os.environ:
		envLibrary.MergeFlags({x: shlex.split(os.environ[x])})

if 'CPPFLAGS' in applyEnvVars and 'CPPFLAGS' in os.environ:
	envLibrary.Append(MODULE_CPPFLAGS = shlex.split(os.environ['CPPFLAGS']))

if 'LDFLAGS' in applyEnvVars and 'LDFLAGS' in os.environ:
	envLibrary.Append(MODULE_LINKFLAGS = shlex.split(os.environ['LDFLAGS']))

if envLibrary['with_debug']:
	envLibrary.Append(CPPFLAGS = ['-g', '-fno-omit-frame-pointer'])

envLibrary.Append(CPPFLAGS = ['-Wall'])
if envLibrary['with_warnerrors']:
	envLibrary.Append(CPPFLAGS = ['-Werror'])

# determine sanitize flags to use
envLibrary.Replace(SANITIZE_FLAGS = [])

if envLibrary['with_debug'] and envLibrary['with_sanitize']:
	envLibrary.Append(SANITIZE_FLAGS = [
		'-fsanitize=address',
		'-fsanitize-address-use-after-scope',
		'-fsanitize=pointer-compare',
		'-fsanitize=pointer-subtract'])
	envLibrary.Append(SANITIZE_FLAGS = [
		'-fsanitize=leak'])
	envLibrary.Append(SANITIZE_FLAGS = [
		'-fsanitize=undefined',
		'-fsanitize=shift',
		'-fsanitize=shift-exponent',
		'-fsanitize=shift-base',
		'-fsanitize=integer-divide-by-zero',
		'-fsanitize=unreachable',
		'-fsanitize=vla-bound',
		'-fsanitize=null',
		'-fsanitize=return',
		'-fsanitize=signed-integer-overflow',
		'-fsanitize=bounds',
		'-fsanitize=bounds-strict',
		'-fsanitize=alignment',
		'-fsanitize=object-size',
		'-fsanitize=float-divide-by-zero',
		'-fsanitize=float-cast-overflow',
		'-fsanitize=nonnull-attribute',
		'-fsanitize=returns-nonnull-attribute',
		'-fsanitize=bool',
		'-fsanitize=enum',
		'-fsanitize=vptr',
		'-fsanitize=pointer-overflow',
		'-fsanitize=builtin'])
	
	if envLibrary['with_sanitize_threads']:
		envLibrary['SANITIZE_FLAGS'].remove('-fsanitize=leak')
		envLibrary['SANITIZE_FLAGS'].remove('-fsanitize=address')
		envLibrary['SANITIZE_FLAGS'].remove('-fsanitize=pointer-compare')
		envLibrary['SANITIZE_FLAGS'].remove('-fsanitize=pointer-subtract')
		envLibrary.Append(SANITIZE_FLAGS = ['-fsanitize=thread'])

# define the targets array and reports dictionary to be filled
parent_targets = {}

configGroup = 'DERemoteLaunchShared'
envLibrary.configReport.add('Treat warnings as errors (dev-builds)', 'with_warnerrors', configGroup)
envLibrary.configReport.add('Build with debug symbols for GDB usage', 'with_debug', configGroup)
envLibrary.configReport.add('Enable run-time optimizations', 'with_optimizations', configGroup)
envLibrary.configReport.add('Enable sanitizer', 'with_sanitize', configGroup)

# build scripts
from config_file_writer import writeConfigFile
from glob_files import globFiles

if envLibrary['TARGET_PLATFORM'] == 'beos':
	envLibrary.Append(LIBS = ['root', 'be'])
elif envLibrary['TARGET_PLATFORM'] == 'windows':
	envLibrary.Append(LIBS = ['kernel32'])

configFileDefines = {}
configFileDefines['OS_UNIX'] = envLibrary['TARGET_PLATFORM'] in ['linux', 'android']
configFileDefines['OS_W32'] = envLibrary['TARGET_PLATFORM'] == 'windows'
configFileDefines['OS_BEOS'] = envLibrary['TARGET_PLATFORM'] == 'beos'
configFileDefines['OS_MACOS'] = envLibrary['TARGET_PLATFORM'] == 'macos'
configFileDefines['WITH_OPTIMIZATIONS'] = envLibrary['with_optimizations']

envLibrary.Configure().Finish()

writeConfigFile(envLibrary.File('src/config.h').srcnode().abspath, configFileDefines)

envLibrary.Append(CPPFLAGS = envLibrary['SANITIZE_FLAGS'])
envLibrary.Append(LINKFLAGS = envLibrary['SANITIZE_FLAGS'])

globalEnv.targetManager.targets['denetwork'].applyBuild(envLibrary)

# library
platformName = 'unix'

if envLibrary['TARGET_PLATFORM'] == 'beos':
	platformName = 'beos'

targetLibrary = globalEnv.targetManager.TargetLibrary('DERemoteLauncher Shared Library')
globalEnv.targetManager.add('deremotelaunchershared', targetLibrary)

sources = []
headers = []
globFiles(envLibrary, 'src', '*.cpp', sources)
globFiles(envLibrary, 'src', '*.h', headers)

objects = [envLibrary.SharedObject(s) for s in sources]

library = envLibrary.StaticLibrary(target='deremotelauncher', source=objects)

targetLibrary.build.extend(library)
targetLibrary.aliasBuild(envLibrary, 'deremotelaunchershared_build')

pathArchive = 'deremotelauncher-{}-x64-{}-sdk'.format(platformName, versionString)

targetLibrary.installCutDirs(envLibrary,
	'deremotelauncher-{}/include/deremotelauncher'.format(versionString),
	headers, 1)

targetLibrary.install.extend(envLibrary.Install(
	'deremotelauncher-{}/lib'.format(versionString), library))

targetLibrary.aliasInstall(envLibrary, 'deremotelaunchershared')

targetLibrary.archiveCutDirs(envLibrary,
	'{}/include/deremotelauncher'.format(pathArchive), headers, 1)

targetLibrary.archiveLibrary(envLibrary, '{}/lib'.format(pathArchive), library)

targetLibrary.addParametersBuildCPP(envLibrary, library,
	'deremotelauncher-{}/include'.format(versionString), linkStatic=True)

# archive
targetArchive = globalEnv.targetManager.TargetArchive('DERemoteLauncher Shared Library Archive')
globalEnv.targetManager.add('deremotelaunchershared_archive', targetArchive)

archiveFiles = {}
archiveFiles['{}/LICENSE'.format(pathArchive)] = envLibrary.File('#LICENSE')
archiveFiles['{}/README.md'.format(pathArchive)] = envLibrary.File('#README.md')

targetArchive.archiveFiles(envLibrary, 'deremotelauncher-{}-x64-{}'.format(platformName, versionString),
	{**targetLibrary.archiveFiles, **archiveFiles},
	format=globalEnv.targetManager.TargetArchive.formatTarBz2)
targetArchive.aliasArchive(envLibrary, 'deremotelaunchershared_archive')
